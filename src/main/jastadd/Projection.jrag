aspect Projection {
  eq DlProjectionExpr.type() {
    TypeDecl   et = getExpr().type();
    Roleish    r  = getRoleExpr().toRoleish();
    Conceptual c;

    if (et.isSemanticTypeDecl()) {
      c = ((SemanticTypeDecl) et).getConcept();
    }
    else {
      c = new Everything();
    }

    Collection<TypeDecl> param = new LinkedList<>();
    String               path  = lookupKnows();

    if (path != null) {
      ClassDecl  ind    = (ClassDecl) lookupType("semantics.model", "Individual");
      Conceptual exists = new Existence(new Inversion(r), c).stripUnknown();
      param.add(ind.semanticTypeDecl(path, exists));
    }
    else {
      param.add(unknownType());
    }

    GenericInterfaceDecl set = (GenericInterfaceDecl) lookupType("java.util", "Set");
    return set.lookupParTypeDecl(param);
  }


  protected boolean DlProjectionExpr.isIndividual(Expr e) {
    return e.type().instanceOf(lookupType("semantics.model", "Individual"));
  }

  protected Problem DlProjectionExpr.errorNotIndividual(Expr e) {
    return errorf("'%s' is not an individual", e.type().typeName());
  }

  DlProjectionExpr contributes errorNotIndividual(getExpr())
    when !isIndividual(getExpr())
    to CompilationUnit.problems();

  DlProjectionExpr contributes errorNotRoleOrString(getRoleExpr())
    when !isRoleOrString(getRoleExpr())
    to CompilationUnit.problems();

  DlProjectionExpr contributes signatureWarning(getRoleExpr().toRoleish())
    when !hasSignature(getRoleExpr().toRoleish(), "role")
    to CompilationUnit.problems();
}


aspect ProjectionBackend {
  syn nta Expr DlProjectionExpr.getTransformed() =
    getKnowBase().mkMethodAccess(
        "project", getExpr().treeCopyNoTransform(), mkModel("Role", getRoleExpr()));

  public void DlProjectionExpr.createBCode(CodeGeneration gen) {
    getTransformed().createBCode(gen);
  }
}
