import semantics.KnowBase;


aspect Typecase {
  eq DlTypecaseStmt.modifiedInScope(Variable var) {
    for (AbstractDlTypecase c : getCasesList()) {
      if (c.modifiedInScope(var)) {
        return true;
      }
    }
    return false;
  }

  syn boolean AbstractDlTypecase.modifiedInScope(Variable var) = getBlock().modifiedInScope(var);


  syn boolean AbstractDlTypecase.isDefaultCase();

  eq DlTypecase       .isDefaultCase() = false;
  eq DlTypecaseDefault.isDefaultCase() = true;


  DlTypecaseStmt contributes each caseProblems() to CompilationUnit.problems();

  syn Collection<Problem> DlTypecaseStmt.caseProblems() {
    Collection<Problem> problems  = new LinkedList<Problem>();
    int                 defaults  = 0;

    for (AbstractDlTypecase c : getCasesList()) {
      if (c.isDefaultCase()) {
        ++defaults;
      }
    }

    if (defaults != 1) {
      problems.add(errorf("typecase has %d defaults, should be exactly 1", defaults));
    }
    else if (!getCases(getNumCases() - 1).isDefaultCase()) {
      problems.add(error("default must be the last block in typecase"));
    }

    return problems;
  }


  public java.util.List<DlTypecase> DlTypecaseStmt.getTypecases() {
    java.util.List<DlTypecase> cases = new LinkedList<DlTypecase>();

    for (AbstractDlTypecase c : getCasesList()) {
      if (!c.isDefaultCase()) {
        cases.add((DlTypecase) c);
      }
    }

    return cases;
  }

  DlTypecaseStmt contributes each subsumationProblems() to CompilationUnit.problems();

  syn Collection<Problem> DlTypecaseStmt.subsumationProblems() {
    Collection<Problem>        problems = new LinkedList<Problem>();
    java.util.List<DlTypecase> cases    = getTypecases();
    String                     knows    = lookupKnows();

    if (!cases.isEmpty() && knows != null) {
      KnowBase       kb   = KnowBase.of(knows);
      Everything     top  = new Everything();
      ConceptualType prev = null;
      Conceptual     pm   = null;

      for (DlTypecase c : cases) {
        ConceptualType cur = c.getType();
        Conceptual     cm  = cur.toModel();

        if (pm != null && kb.isSubtype(cm, pm)) {
          String pprev = prev.prettyPrint();
          String pcur  = cur .prettyPrint();
          problems.add(errorf("bad typecase order, '%s' subsumes '%s'", pprev, pcur));
        }

        if (!kb.isSatisfiable(cm)) {
          String pcur = cur.prettyPrint();
          problems.add(errorf(
              "typecase '%s' is unsatisfiable and will never match", pcur));
        }
        else if (kb.isSubtype(top, cm)) {
          String pcur = cur.prettyPrint();
          problems.add(errorf(
              "typecase '%s' will always match, use the default case instead", pcur));
        }

        prev = cur;
        pm   = cm;
      }
    }

    return problems;
  }
}
